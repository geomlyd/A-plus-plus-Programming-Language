function generalLoop(elist1, expr, elist2, f, idList) {	
	
	eval("toReturn = <<;>>; for (" + #(elist1) + "; " + #(expr) + "; " + #(elist2) + ") {
		if (typeOf(idList) != \"undefined\")
			toReturn = << ~toReturn; ~(f(~idList)); >>;
		else
			toReturn = << ~toReturn; f(); >>;
	}");

	return toReturn;
}

bsInnerLoopAST = (
	function bsInnerLoop (j) {
		return <<
			if (array[~j] > array[~j+1]) {
				tmp = array[~j];
				array[~j] = array[~j+1];
				array[~j+1] = tmp;
			}
		>>;
	}
);

bsOuterLoopAST = (
	function bsOuterLoop (i, n) {		
		loop = generalLoop(<<j = 0>>, <<j < ~n - ~i - 1>>, <<j++>>, bsInnerLoopAST, <<j>>);
		return loop;
	}
);

function bubbleSortN(array, n) {	
	return generalLoop(<<i = 0>>, <<i < ~n - 1>>, <<i++>>, bsOuterLoopAST, <<i, ~n>>);	
}

x = [6,3,4,8,0,1,2,5,7];
y = [1,8,3,7,2,5,4,6,0];
z = [30,60,80,40,10,90,50,20,70,0];

bubbleSort9 = bubbleSortN(x, objectSize(x));
array = x;
print("unorted array: ", array, "\n");
!(bubbleSort9);
print("sorted array:  ", array, "\n\n");

array = y;
print("unorted array: ", array, "\n");
!(bubbleSort9);
print("sorted array:  ", array, "\n\n");

bubbleSort10 = bubbleSortN(z, objectSize(z));
array = z;
print("unorted array: ", array, "\n");
!(bubbleSort10);
print("sorted array:  ", array, "\n");

